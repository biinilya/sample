# Copyright 2010 The Go Authors. All rights reserved.
# Use of this source code is governed by a BSD-style
# license that can be found in the LICENSE file.

package filter

type FilterPeg Peg {
  AST
}

e <- sp e1 !.
e1 <- e2 ( and e2 { p.AddOperator(OpAnd) } / or e2 { p.AddOperator(OpOr) } )*
e2 <- not value { p.AddOperator(OpNot) } / value
value <- expression sp / open e1 close

and <- 'AND' sp / 'and' sp
or <- 'OR' sp  / 'or' sp
not <- 'NOT' sp / 'not' sp

open <- '(' sp
close <- ')' sp

gt <- 'gt' sp { p.AddComparator(CmpGt) }
lt <- 'lt' sp { p.AddComparator(CmpLt) }
eq <- 'eq' sp { p.AddComparator(CmpEq) }
ne <- 'ne' sp { p.AddComparator(CmpNe) }

quote <- '\''

expression <- (identifier sp comparator sp parameter) { p.AddExpression() }
comparator <- gt / lt / eq / ne
identifier <- <[a-zA-Z][a-zA-Z0-9_]*> { p.AddIdentifier(buffer[begin:end]) }
p_string <- quote <[^']+> quote { p.AddArgument(TypeString, buffer[begin:end]) }
p_number <- <[0-9]+> { p.AddArgument(TypeInt, buffer[begin:end]) }
parameter <- p_string / p_number
sp <- ( ' ' / '\t' )*
